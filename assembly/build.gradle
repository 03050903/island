apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.0'

    defaultConfig {
        applicationId "com.oasisfeng.island"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 11
        versionName "2.0 beta 5"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    flavorDimensions "packaging"

    productFlavors {
        engine {    // Use the same application ID to retain the profile / device ownership between "engine" and "full" build.
            dimension "packaging"
        }
        mobile {
            applicationIdSuffix ".mobile"
            dimension "packaging"
        }
        full {
            dimension "packaging"
        }
    }

    // Use the release signing certificate (altered to align with normal Android debug keystore) for debug build
    // to retain the profile / device ownership between "debug" and "release" build.
    signingConfigs.debug.storeFile file("../island.debug.keystore")

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        htmlReport false
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    repositories.flatDir {
        dirs '../app/libs'
    }
}

dependencies {
    // Engine only
    engineImplementation project(':engine')
    // Mobile only
    mobileImplementation project(':mobile')
    // Full
    fullImplementation project(':engine')
    fullImplementation project(':mobile')

    implementation 'com.android.support:support-v4:25.3.1'     // To workaround the dependency conflict caused by "com.android.databinding:library"
}
