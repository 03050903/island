apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.oasisfeng.island"
        minSdkVersion 21
        targetSdkVersion 26
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    dataBinding.enabled true

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
        }
    }

    flavorDimensions "packaging"

    productFlavors {
        engine {    // Use the same application ID to retain the profile / device ownership between "engine" and "full" build.
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        mobile {
            applicationIdSuffix ".mobile"
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        fileprovider {
            applicationIdSuffix ".fileprovider"
            dimension "packaging"
            matchingFallbacks = ['full']
        }
        full {
            dimension "packaging"
        }
    }

    // Use the release signing certificate (altered to align with normal Android debug keystore) for debug build
    // to retain the profile / device ownership between "debug" and "release" build.
    signingConfigs.debug.storeFile file("../island.debug.keystore")

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        check 'NewApi'
        abortOnError true
        htmlReport false
        xmlReport false
        textReport true
        textOutput "stdout"
    }

    repositories.flatDir {
        dirs '../app/libs'
    }
}

dependencies {
    // Engine only
    engineImplementation project(':engine')
    engineImplementation project(':permission')
    // Mobile only
    mobileImplementation project(':mobile')
    // File Provider only
    fileproviderImplementation project(':fileprovider')
    // Full
    fullImplementation project(':engine')
    fullImplementation project(':mobile')
    fullImplementation project(':permission')
    fullImplementation project(':fileprovider')

    implementation 'com.android.support:support-v4:26.1.0'          // To workaround the dependency conflict caused by "com.android.databinding:library"
    implementation 'com.google.firebase:firebase-analytics:11.2.2'  // To workaround the dependency conflict caused by "com.google.gms.google-services"
}

// Firebase plug-in
apply plugin: 'com.google.gms.google-services'
